unit Digraph;

interface

uses System.Types, DynStructures, System.SysUtils;

type

  // Тип списка смежности
  TPAdjList = TPList;

  // Тип вершины графа
  TPNode = ^TNode;

  TNode = record
    Center: TPoint;
    Number: Cardinal;
    Head: TPAdjList;
    Next: TPNode;
  end;

  // Тип простой граф
  TGraph = record
    Head: TPNode;
    Tail: TPNode;
    Order: Cardinal;
  end;

  { Функция нахождения вершины и её координаты по точке на холсте }
procedure Centralize(const G: TGraph; var P: TPoint; out u: Cardinal);

{ Функция получения центра вершины }
function GetCenter(const G: TGraph; u: Cardinal): TPoint;

{ Функция вычисления расстояния между двумя точками в пикселях }
function Distance(const p1, p2: TPoint): Integer;

{ Процедура добавления вершины в граф }
procedure AddNode(var G: TGraph; const C: TPoint);

{ Процедура добавления рёбер в граф }
procedure AddLink(var G: TGraph; u, v: Cardinal);

{ Процедура инициализации графа }
procedure InitializeGraph(var G: TGraph);

{ Процедура очищения графа }
procedure DestroyGraph(var G: TGraph);

{ Функция поиска в глубину }
function DFS(const G: TGraph; u, v: Cardinal): TPList;

{ Функция поиска в ширину }
function BFS(const G: TGraph; u, v: Cardinal): TPList;

implementation

function Distance(const p1, p2: TPoint): Integer;
begin
  Result := Round(Sqrt(Sqr(p2.x - p1.x) + Sqr(p2.y - p1.y)));
end;

{ Функция нахождения вершины по номеру в списке }
function GetByNumber(const G: TGraph; u: Integer): TPNode;
begin
  Result := G.Head;

  // Цикл А1. Поиск вершины с данным номером
  while Result.Number <> u do
  begin
    Result := Result.Next;
  end; // Конец А1

end;

procedure Centralize;
var
  Node: TPNode;
  Found: TPNode;
  isFound: Boolean;
begin
  Node := G.Head;
  isFound := False;
  Found := nil;

  // Цикл А1. Поиск последней вершины с близкими координатами
  while Node <> nil do
  begin
    isFound := Distance(Node.Center, P) <= 20;
    if isFound then
      Found := Node;
    Node := Node.Next;
  end; // Конец А1

  // Возврат найденной вершины
  if Found <> nil then
  begin
    u := Found.Number;
    P := Found.Center;
  end
  else
    u := 0; // Вершина не была найдена
end;

function GetCenter;
var
  Node: TPNode;
begin
  Node := GetByNumber(G, u);
  Result := Node.Center;
end;

procedure AddNode;
var
  Node: TPNode;
begin
  Inc(G.Order);

  // Инициализация новой вершины
  New(Node);
  with Node^ do
  begin
    Center := C;
    Number := G.Order;
    Head := nil;
    Next := nil;
  end;

  // Инициализация указателя на новую вершину
  if G.Head = nil then
    G.Head := Node
  else
    G.Tail.Next := Node;

  // Перезапись хвоста
  G.Tail := Node;

end;

procedure AddLink;
var
  Node: TPNode;
  Prev, Curr: TPList;
  isFound: Boolean;
begin

  // Получение вершины
  Node := GetByNumber(G, u);

  // Инициализация нового соседа
  New(Curr);
  Curr.Elem := v;

  // Получение начального соседа
  Prev := Node.Head;

  // Список смежности был пуст или голова была больше нового соседа
  if (Prev = nil) or (Prev.Elem > v) then
    Push(Node.Head, v)
  else
  begin

    // Цикл А1. Поиск места вставки (предыдущего соседа)
    isFound := (Prev.Next = nil) or (Prev.Next.Elem > v);
    while not isFound do
    begin
      Prev := Prev.Next;
      isFound := (Prev.Next = nil) or (Prev.Next.Elem > v);
    end;

    // Вставка нового соседа
    Curr.Next := Prev.Next;
    Prev.Next := Curr;

  end; // Конец if

end;

procedure InitializeGraph;
begin
  G.Head := nil;
  G.Tail := nil;
  G.Order := 0;
end;

procedure DestroyGraph;
var
  Node: TPNode;
  Neighbour: TPAdjList;
begin

  // Цикл А1. Освобождение списка вершин
  while G.Head <> nil do
  begin
    Node := G.Head;

    // Цикл А2. Освобождение списка соседей вершины
    while Node.Head <> nil do
    begin
      Neighbour := Node.Head;
      Node.Head := Node.Head.Next;
      Dispose(Neighbour);
    end; // Конец А2

    G.Head := G.Head.Next;
    Dispose(Node);
  end; // Конец А1
end;

function RestorePath(const Parents: array of Cardinal; u, v: Integer): TPList;
begin

  InitializeStack(Result); // Инициализация стека

  // Цикл А1. Добавление очередного предка в пути
  while u <> v do
  begin
    Push(Result, v);
    v := Parents[v - 1];
  end; // Конец А1
  Push(Result, u);

end;

function DFS;
var
  Node: TPNode;
  Neighbour: TPAdjList;
  Stack: TStack;
  isFound: Boolean;
  Parents: Array of Cardinal;
  isVisited: Array of Boolean;
  i: Integer;
  w, StartCopy: Cardinal;
begin

  // Инициализация стека
  InitializeStack(Stack);
  Push(Stack, u);

  // Инициализация массива меток
  SetLength(isVisited, G.Order);
  for i := Low(isVisited) to High(isVisited) do
    isVisited[i] := False;

  // Инциализация массива предков
  SetLength(Parents, G.Order);

  // Цикл А1. Извлечение элементов из стека до нахождения вершины
  StartCopy := u; // Сохранение начала пути
  isFound := u = v;
  while not(isEmpty(Stack) or isFound) do
  begin
    // Получение вершины
    u := Pop(Stack);
    if not isVisited[u - 1] then
    begin

      // Посещение вершины
      isVisited[u - 1] := true;
      isFound := u = v;

      // Цикл А2. Сохранение соседей в стек
      Node := GetByNumber(G, u);
      Neighbour := Node.Head;
      while not((Neighbour = nil) or isFound) do
      begin
        w := Neighbour.Elem; // Получение очередного соседа

        // Добавление в стек непосещённых вершин
        if not isVisited[w - 1] then
        begin
          Push(Stack, w); // Вставка вершины в стек
          Parents[w - 1] := u; // Сохранение пути
        end; // Конец if

        Neighbour := Neighbour.Next; // Переход к следующему соседу
      end; // Конец А2

    end; // Конец if

  end; // Конец А1

  DestroyStack(Stack); // Очистка стека

  // Восстановление пути
  InitializeStack(Result);
  if isFound then
    Result := RestorePath(Parents, StartCopy, v);
end;

function BFS;
var
  Node: TPNode;
  Neighbour: TPAdjList;
  Queue: TQueue;
  i: Integer;
  isVisited: Array of Boolean;
  Parents: Array of Cardinal;
  StartCopy: Cardinal;
  w: Cardinal;
  isFound: Boolean;
begin
  // Получение информации о начальной вершине
  Node := GetByNumber(G, u);

  // Инициализация очереди
  InitializeQueue(Queue);
  Enqueue(Queue, u);

  // Инициализация массива меток
  SetLength(isVisited, G.Order);
  for i := Low(isVisited) to High(isVisited) do
    isVisited[i] := False;

  // Инициализация массива предков
  SetLength(Parents, G.Order);

  // Цикл А1. Извлечение из очереди вершин
  StartCopy := u;
  isFound := u = v;
  while not(isEmpty(Queue.Head) or isFound) do
  begin
    u := Dequeue(Queue);
    if not isVisited[u - 1] then
    begin

      // Посещение вершины
      isVisited[u - 1] := true;
      isFound := u = v;

      // Цикл А2. Вставка в очередь соседей вершины
      Node := GetByNumber(G, u);
      Neighbour := Node.Head;
      while not((Neighbour = nil) or isFound) do
      begin
        w := Neighbour.Elem;
        if not isVisited[w - 1] then
        begin
          Enqueue(Queue, w); // Запись в очередь
          Parents[w - 1] := u; // Сохранение пути
        end; // Конец if
        Neighbour := Neighbour.Next;
      end; // Конец А2

    end;
  end;
end;

end.
